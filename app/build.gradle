apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

boolean dev = config_dev.toBoolean()
if (!dev) {
    apply plugin: 'com.growingio.android'
}

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    defaultConfig {
        applicationId "cn.dong.cisample"
        if (dev) {
            // To avoid using legacy multidex when building from the command line,
            // set minSdkVersion to 21 or higher. When using Android Studio 2.3 or higher,
            // the build automatically avoids legacy multidex when deploying to a device running
            minSdkVersion 21
        } else {
            minSdkVersion MIN_SDK_VERSION as int
        }
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode generateVersionCode()
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            keyAlias 'sample'
            keyPassword 'password'
            storeFile file('../keystore.jks')
            storePassword 'password'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'deployment_environment'
    productFlavors {
        staging {
            applicationIdSuffix '.staging'

        }
        production {

        }
    }

    applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def checkTask = task "buildCheck${variantName}" {
            doLast {
                if (variant.buildType.name == 'release' && dev) {
                    throw new GradleException("The release version can not enable dev")
                }
            }
        }
        variant.preBuild.dependsOn checkTask
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    //use to test multiDex
    implementation 'com.google.guava:guava:19.0'
    implementation 'org.scala-lang:scala-library:2.11.7'
    implementation "com.growingio.android:vds-android-agent:${growingio_version}@aar"
}

/**
 * 用 git commit 的数量作为应用 VersionCode，使 VersionCode 实现自增长，并且与代码版本保持一致性
 */
def generateVersionCode() {
    return gitCommitCount()
}

def gitCommitCount() {
    try {
        def count = "git rev-list --count HEAD".execute(null, project.rootDir).text.trim()
        if (count == null) {
            throw new GradleException("can't get git rev, you should add git to system path")
        }
        return count.toInteger()
    } catch (Exception ignored) {
        throw new GradleException("can't get git rev, you should add git to system path")
    }
}
